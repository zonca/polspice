pro read_spice, file, cl, mult=l, theta = theta, llfact=fl
;+
;
; READ_SPICE, file, cl, mult=mult, theta=theta, llfact=llfact
;
;  reads C(theta) (angular correlation) and C(l) (angular power spectrum) 
;  files generated by PolSpice, both in plain ASCII and FITS format
;
; INPUT:
;    File: character string containing the file to be read
;
; OUTPUTS:
;   cl: contains the C(l) or C(theta) read from the file
;
;   llfact: factor l*(l+1)/(2 Pi)  usually applied to C(l) upon plotting
;
;   mult:    multipole l read from the C(l) file
;
;   theta: angle in Radians read from the C(theta) file
;
; EXAMPLES:
;  - to read an (ASCII) angular correlation file named cor.txt
;    read_spice, 'cor.txt', cor, theta=theta
;    plot, theta*!radeg, cor[*,0]
;
;  - to read a (FITS) angular correlation file named cor.fits
;    read_spice, 'cor.fits', cor, theta=theta
;    plot, theta*!radeg, cor[*,0]
;
;  - to read an (ASCII) angular power spectrm file named cl.txt
;    read_spice, 'cl.txt', cl, mult=l, llfact=fl
;    plot, l, fl*cl[*,0]
;
;  - to read a (FITS) angular correlation file named cl.fits
;    read_spice, 'cl.fits', cl, mult=l, llfact=fl
;    plot, l, fl*cl[*,0]
;
;
; HISTORY
;   v1.0 ??
; 2016-03-09:  added header, ready for 9-column ASCII files
;
;-


junk = headfits(file, errmsg=errmsg,/silent)

if (errmsg ne '') then begin
; ascii file

    head=' ' & fline = ' '
    openr,lun,file,/get_lun

    ; read file parameters
    readf,lun,head
    b=strpos(head,'=')
    head = strmid(head,b+1)
    args = long(strsplit(head,' ',/extract))
    nlmax = args[0] & ncor = args[1] & nside = args[2]

    ; find out number of columns
    readf,lun,fline
    fields = strsplit(fline,' ',/extract)
    nf = n_elements(fields)
    free_lun,lun

    case (nf - ncor) of
        1: begin
            readcol, file, l,         cltt, clee, clbb,  clte, cltb, cleb,  clet, clbt, clbe ; ang. power spectrum
            fl = l*(l+1.d0)/(2*!dpi)
        end
        2: readcol,  file, theta, ct, cltt, clee, clbb,  clte, cltb, cleb,  clet, clbt, clbe ; ang. correlation
        else: begin
            message,'wrong number of fields in file '+file
        end
    endcase

    case ncor of
        1: cl = cltt ; T only
        4: cl = [[cltt],[clee],[clbb], [clte]]
        6: cl = [[cltt],[clee],[clbb], [clte],[cltb],[cleb]]
        9: cl = [[cltt],[clee],[clbb], [clte],[cltb],[cleb], [clet],[clbt],[clbe]]
        else: begin
            message,'unable to read file '+file
        end
    endcase

endif else begin
; FITS file
    st=mrdfits(file,1,/silent)

    junk = where(tag_names(st) eq 'ANGLE', nj)
    if (nj gt 0) then begin
                                ; ang. correlation
        offset = 1
        na = n_elements(st.(0))
        theta = st.angle

    endif else begin
                                ; ang. power spectrum
        offset = 0
        na = n_elements(st.(0))
        l = dindgen(na)
        fl = l*(l+1.d0)/(2*!dpi)

    endelse

    nf = n_tags(st)-offset
    cl = dblarr(na, nf)
    for i=0, nf-1 do begin
        cl[0,i] = st.(i+offset)
    endfor

endelse

return
end

